buildscript {
    ext.kotlin_version = '1.3.50'

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "guru.stefma.androidartifacts:androidartifacts:1.4.0"
        classpath "guru.stefma.bintrayrelease:bintrayrelease:1.1.2"
    }
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
}

apply plugin: 'kotlin'
apply plugin: "guru.stefma.artifacts"

group = 'com.morallito.gitversioner'
version = '0.5.1'

dependencies {
    compile gradleApi()
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.12.0'
    testCompile 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0'
}
repositories {
    jcenter()
}

javaArtifact {
    artifactId = 'gitversioner'
    name = "Gitversioner gradle plugin"
    url = 'https://github.com/passsy/gradle-gitVersioner-plugin'
    license {
        name = "Apache License, Version 2.0"
        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
    }
    pom {
        developers {
            developer {
                id = "passsy"
                name = "Pascal Welsch"
                url = 'https://github.com/passsy'
            }
        }
    }
}

// ./gradlew bintrayUpload
apply plugin: "guru.stefma.bintrayrelease"
publish {
    userOrg = 'passsy'
    desc = "Extracts a useful versionCode and versionName from git history"
    website = 'https://github.com/passsy/gradle-gitVersioner-plugin'
}


task saveVersionToResources(type: WriteProperties) {
    comment = "gitversioner gradle plugin version"
    property('version', version)
    outputFile = file(sourceSets.main.output.resourcesDir.absolutePath + "/version.properties")
}
processResources.dependsOn(saveVersionToResources)
